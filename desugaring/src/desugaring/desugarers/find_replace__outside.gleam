import desugaring/authoring
import desugaring/core.{
  type Desugarer, type DesugarerTransform, type DesugaringError,
}
import desugaring/nodemaps_2_transform as n2t

pub const name = "find_replace__outside"

// 🏖️🏖️🏖️🏖️🏖️🏖️🏖️🏖️🏖️🏖️🏖️🏖️
// 🏖️🏖️ Desugarer 🏖️🏖️
// 🏖️🏖️🏖️🏖️🏖️🏖️🏖️🏖️🏖️🏖️🏖️️️️️🏖️

/// Replaces literal text outside the forbidden subtrees.
pub fn constructor(param: Param, outside: List(String)) -> Desugarer {
  authoring.desugarer_with_outside(
    name: name,
    param: param,
    outside: outside,
    prepare: param_to_inner_param,
    transform: inner_param_to_transform,
  )
}

type Param =
  #(
    // Text to replace.
    String,
    // Replacement text.
    String,
  )

type InnerParam {
  InnerParam(from: String, to: String)
}

fn param_to_inner_param(param: Param) -> Result(InnerParam, DesugaringError) {
  Ok(InnerParam(param.0, param.1))
}

fn inner_param_to_transform(
  inner: InnerParam,
  outside: List(String),
) -> DesugarerTransform {
  let nodemap: n2t.OneToOneNoErrorNodemap = core.find_replace_if_t(
    _,
    inner.from,
    inner.to,
  )
  nodemap
  |> n2t.one_to_one_no_error_nodemap_2_desugarer_transform_with_forbidden(
    outside,
  )
}

// 🌊🌊🌊🌊🌊🌊🌊🌊🌊🌊🌊🌊
// 🌊🌊🌊 tests 🌊🌊🌊🌊
// 🌊🌊🌊🌊🌊🌊🌊🌊🌊🌊🌊🌊

fn assertive_tests_data() -> List(core.AssertiveTestDataWithOutside(Param)) {
  [
    core.AssertiveTestDataWithOutside(
      param: #("from", "to"),
      outside: ["keep_out"],
      source: "
                <> root
                  <> A
                    <> B
                      <>
                        'from a thing'
                        'to a thing'
                      <> keep_out
                        <>
                          'from a thing'
                          'to a thing'
                    <> keep_out
                      <> B
                        <>
                          'from a thing'
                          'to a thing'
                ",
      expected: "
                <> root
                  <> A
                    <> B
                      <>
                        'to a thing'
                        'to a thing'
                      <> keep_out
                        <>
                          'from a thing'
                          'to a thing'
                    <> keep_out
                      <> B
                        <>
                          'from a thing'
                          'to a thing'
                ",
    ),
  ]
}

pub fn assertive_tests() {
  core.assertive_test_collection_from_data_with_outside(
    name,
    assertive_tests_data(),
    constructor,
  )
}
