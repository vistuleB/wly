import gleam/dict.{type Dict}
import gleam/list
import gleam/option.{type Option, Some, None}
import gleam/string
import infrastructure.{type Desugarer, Desugarer, type DesugarerTransform, type DesugaringError, DesugaringError} as infra
import nodemaps_2_desugarer_transforms as n2t
import vxml.{type Attribute, type VXML, Attribute, V}
import blame.{type Blame} as bl
import splitter as sp
import on

fn grand_wrapper_attributes(
  handles: HandlesDict,
) -> List(Attribute) {
  handles
  |> dict.map_values(
    fn (key, value) {
      let #(value, id, path, _) = value
      Attribute(desugarer_blame(19), "handle", key <> "|" <> value <> "|" <> id <> "|" <> path)
    }
  )
  |> dict.values
}

fn try_read_handle(
  attr: Attribute
) -> Result(#(String, String, String), DesugaringError) {
  assert attr.key == "handle"
  case string.split(attr.value, "|") {
    [name, value, id] -> Ok(#(name, value, id))
    _ -> Error(DesugaringError(attr.blame, "handle attribute not in form <name>|<value>|<id>; found: â€œ" <> attr.value <> "â€"))
  }
}

fn try_insert_handle(
  handles: HandlesDict,
  name: String,
  value: String,
  id: String,
  path: String,
  blame: Blame,
) -> Result(HandlesDict, DesugaringError) {
  case dict.get(handles, name) {
    Ok(entry) ->
      Error(DesugaringError(blame, "redefinition of '" <> name <> "' (previously defined at " <> bl.blame_digest(entry.3) <> ")"))
    Error(_) ->
      Ok(dict.insert(handles, name, #(value, id, path, blame)))
  }
}

fn attributes_fold(
  not_handles_acc: List(Attribute),
  remaining: List(Attribute),
  handles: HandlesDict,
  path: String,
) -> Result(#(HandlesDict, List(Attribute)), DesugaringError) {
  use first, rest <- on.empty_nonempty(
    remaining,
    Ok(#(handles, not_handles_acc |> list.reverse))
  )

  case first.key {
    "handle" -> {
      use #(name, value, id) <- on.ok(try_read_handle(first))
      use handles <- on.ok(try_insert_handle(handles, name, value, id, path, first.blame))
      attributes_fold(not_handles_acc, rest, handles, path)
    }
    _ -> attributes_fold([first, ..not_handles_acc], rest, handles, path)
  }
}

fn check_no_handles(
  attrs: List(Attribute)
) -> Result(List(Nil), DesugaringError) {
  list.try_map(
    attrs,
    fn(attr) {
      case attr.key == "handle" {
        True -> {
          let #(name, _, _) = sp.split(sp.new(["|"]), attr.value)
          Error(DesugaringError(attr.blame, "no page path at handle '" <> name <> "'"))
        }
        False -> Ok(Nil)
      }
    }
  )
}

fn update_path(
  state: State,
  node: VXML,
  inner: InnerParam,
) -> State {
  let assert V(_, _, _, _) = node
  case infra.v_first_attribute_with_key(node, inner) {
    Some(Attribute(_, _, value)) -> State(..state, path: Some(value))
    None -> state
  }
}

fn t_transform(
  vxml: VXML,
  state: State,
) -> Result(#(VXML, State), DesugaringError) {
  Ok(#(vxml, state))
}

fn v_before_transforming_children(
  vxml: VXML,
  state: State,
  inner: InnerParam,
) -> Result(#(VXML, State), DesugaringError) {
  let assert V(_, _, attrs, _) = vxml
  let state = update_path(state, vxml, inner)
  case state.path {
    None -> {
      use _ <- on.ok(check_no_handles(attrs))
      Ok(#(vxml, state))
    }
    Some(path) -> {
      use #(handles, attrs) <- on.ok(attributes_fold([], attrs, state.handles, path))
      Ok(#(V(..vxml, attributes: attrs), State(..state, handles: handles)))
    }
  }
}

fn v_after_transforming_children(
  vxml: VXML,
  ancestors: List(VXML),
  original_state: State,
  state: State,
) -> Result(#(VXML, State), DesugaringError) {
  let state = State(..state, path: original_state.path)
  case list.is_empty(ancestors) {
    False -> Ok(#(vxml, state))
    True -> {
      let grand_wrapper = V(
        desugarer_blame(138),
        "GrandWrapper",
        grand_wrapper_attributes(state.handles),
        [vxml],
      )
      Ok(#(grand_wrapper, state))
    }
  }
}

fn nodemap_factory(inner: InnerParam) -> n2t.FancyOneToOneBeforeAndAfterStatefulNodeMap(State) {
   n2t.FancyOneToOneBeforeAndAfterStatefulNodeMap(
    v_before_transforming_children: fn(vxml, _, _, _, _, state) {
      v_before_transforming_children(vxml, state, inner)
    },
    v_after_transforming_children: fn(vxml, ancestors, _, _, _, original_state, latest_state) {
      v_after_transforming_children(vxml, ancestors, original_state, latest_state)
    },
    t_nodemap: fn(vxml, _, _, _, _, state) {
      t_transform(vxml, state)
    },
  )
}

fn transform_factory(inner: InnerParam) -> infra.DesugarerTransform {
  n2t.fancy_one_to_one_before_and_after_stateful_nodemap_2_desugarer_transform(
    nodemap_factory(inner),
   State(dict.new(), None),
  )
}

fn param_to_inner_param(param: Param) -> Result(InnerParam, DesugaringError) {
  Ok(param)
}

type HandlesDict = Dict(String, #(String,       String,     String,      Blame))
//                      â†–         â†–             â†–           â†–
//                      handle    string value  handle id   page path
//                      name      of handle     on page     for handle

type State {
  State(
    handles: HandlesDict,
    path: Option(String),
  )
}

type Param = String
//           â†–
//           key of attribute
//           holding the local path (could be 'path' or 'page', etc)
type InnerParam = Param

pub const name = "handles_and_ids_generate_dictionary"
fn desugarer_blame(line_no: Int) { bl.Des([], name, line_no) }

// ðŸ–ï¸ðŸ–ï¸ðŸ–ï¸ðŸ–ï¸ðŸ–ï¸ðŸ–ï¸ðŸ–ï¸ðŸ–ï¸ðŸ–ï¸ðŸ–ï¸ðŸ–ï¸
// ðŸ–ï¸ðŸ–ï¸ Desugarer ðŸ–ï¸ðŸ–ï¸
// ðŸ–ï¸ðŸ–ï¸ðŸ–ï¸ðŸ–ï¸ðŸ–ï¸ðŸ–ï¸ðŸ–ï¸ðŸ–ï¸ðŸ–ï¸ðŸ–ï¸ðŸ–ï¸
//------------------------------------------------53
/// Looks for `handle` attributes in the V nodes
/// that are expected to be in form
///
/// `handle=name | value | id`
///
/// (Panics if not in this form.)
///
/// Transform the values into a dict where the key
/// is the handle name and the values are tuples
/// #(String, String, String) comprising the handle,
/// id, and value.
///
/// Adds new field of data (path) which represents
/// the filename and is expected to be available
/// In attribute value of node with Param.0 tag
/// Param.1 attribute_key.
///
/// Wraps the document root by a V node with tag
/// GrandWrapper and transform back the dict as the
/// grandwrapper's attributes.
///
/// Returns a pair of newly created node and state
/// of handles used to check for name uniqueness.
///
/// Throws error if
/// 1. there are multiple handles with same
///    name
/// 2. no node found with Param.0 tag Param.1
///    attribute_key
pub fn constructor(param: Param) -> Desugarer {
  Desugarer(
    name: name,
    stringified_param: option.None,
    stringified_outside: option.None,
    transform: case param_to_inner_param(param) {
      Error(error) -> fn(_) { Error(error) }
      Ok(inner) -> transform_factory(inner)
    },
  )
}

// ðŸŒŠðŸŒŠðŸŒŠðŸŒŠðŸŒŠðŸŒŠðŸŒŠðŸŒŠðŸŒŠðŸŒŠðŸŒŠðŸŒŠ
// ðŸŒŠðŸŒŠðŸŒŠ tests ðŸŒŠðŸŒŠðŸŒŠðŸŒŠðŸŒŠ
// ðŸŒŠðŸŒŠðŸŒŠðŸŒŠðŸŒŠðŸŒŠðŸŒŠðŸŒŠðŸŒŠðŸŒŠðŸŒŠðŸŒŠ
fn assertive_tests_data() -> List(infra.AssertiveTestData(Param)) {
  [
    infra.AssertiveTestData(
      param: "local_path",
      source:   "
                <> root
                  <> ChapterChapter
                    local_path=./ch1.html
                    <>
                      \"some text\"
                    <> Math
                      handle=super-name|AA|_23-super-id
                      <>
                        \"$x^2 + b^2$\"
                ",
      expected: "
                <> GrandWrapper
                  handle=super-name|AA|_23-super-id|./ch1.html
                  <> root
                    <> ChapterChapter
                      local_path=./ch1.html
                      <>
                        \"some text\"
                      <> Math
                        <>
                          \"$x^2 + b^2$\"
                "
    ),
  ]
}

pub fn assertive_tests() {
  infra.assertive_test_collection_from_data(name, assertive_tests_data(), constructor)
}
